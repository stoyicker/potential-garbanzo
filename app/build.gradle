buildscript {
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath(libs.bundles.gradlePluginsAndroid)
    classpath(libs.bundles.gradlePluginsKotlin)
  }
}
apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"

final def runningOnCi = System.getenv("CI") != null
android {
  compileSdkVersion 31
  buildToolsVersion "31.0.0"
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  defaultConfig {
    applicationId "org.interview.jorge"
    minSdkVersion 16
    targetSdkVersion 31
    // Depending on the git method locally causes IDE issues
    versionCode runningOnCi ? "git rev-list --count HEAD".execute().text.toInteger() : Integer.MAX_VALUE
    versionName String.valueOf(versionCode)
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    testInstrumentationRunnerArguments clearPackageData: "true"
  }
  variantFilter {
    it.ignore = !it.buildType.isDebuggable()
  }
  lintOptions {
    quiet false
    abortOnError true
    checkAllWarnings true
    warningsAsErrors true
    noLines false
    explainIssues true
    xmlReport false
    htmlReport true
    checkTestSources true
    checkDependencies false
    ignore "MediaCapabilities" // https://issuetracker.google.com/issues/195242930
  }
}

repositories {
  google()
  mavenCentral()
}

dependencies {
  implementation(libs.bundles.kotlin)

  testImplementation(libs.bundles.junit)

  androidTestImplementation(libs.bundles.espresso)
  androidTestImplementation(libs.bundles.androidxTest)

  androidTestUtil(libs.bundles.androidxTestOrchestrator)
}
